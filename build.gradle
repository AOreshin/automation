import java.time.Duration

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    id 'io.codearte.nexus-staging' version '0.21.1'
    id 'net.nemerosa.versioning' version '2.8.2'
}

allprojects {
    group = "com.github.aoreshin"
    version = versioning.info.full
}

nexusStaging {
    numberOfRetries = 500
}

subprojects {
    apply plugin: 'de.marcphilipp.nexus-publish'
    apply plugin: 'signing'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    nexusPublishing {
        Duration duration = Duration.ofMinutes(5)
        connectTimeout = duration
        clientTimeout = duration

        repositories {
            sonatype {
                username = "${nexusUsername}"
                password = "${nexusPassword}"
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version

                from components.java

                pom {
                    name = project.name
                    description = 'Project to ease pain of test automation'
                    url = 'https://github.com/AOreshin/automation'
                    inceptionYear = '2019'
                    licenses {
                        license {
                            name = 'GNU General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'AOreshin'
                            name = 'Alexander Oreshin'
                            email = 'hummr65@gmail.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/AOreshin/automation'
                        connection = 'scm:https://github.com/AOreshin/automation.git'
                        developerConnection = 'scm:https://github.com/AOreshin/automation.git'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    repositories {
        mavenCentral()
    }

    sourceCompatibility = 11

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"
    }

    test {
        useJUnitPlatform()

        systemProperties['junit.jupiter.execution.parallel.enabled'] = true
        systemProperties['junit.jupiter.execution.parallel.mode.default'] = 'concurrent'
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("${buildDir}/jacocoHtml")
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.9
                }
            }
        }
    }
}