buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1"
    }
}

version = '1.0.0.3'

subprojects {
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'io.codearte.nexus-staging'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    group = "com.github.aoreshin"
    version = rootProject.version
    sourceCompatibility = 11

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"
    }

    test {
        useJUnitPlatform()

        systemProperties['junit.jupiter.execution.parallel.enabled'] = true
        systemProperties['junit.jupiter.execution.parallel.mode.default'] = 'concurrent'
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("${buildDir}/jacocoHtml")
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.9
                }
            }
        }
    }

    modifyPom {
        project {
            name = project.name
            description = project.description
            url 'https://github.com/AOreshin/automation'
            inceptionYear '2019'

            scm {
                url 'https://github.com/AOreshin/automation'
                connection 'scm:https://github.com/AOreshin/automation.git'
                developerConnection 'scm:https://github.com/AOreshin/automation.git'
            }

            licenses {
                license {
                    name 'GNU General Public License v3.0'
                    url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'AOreshin'
                    name 'Alexander Oreshin'
                    email 'hummr65@gmail.com'
                }
            }
        }
    }

    extraArchive {
        sources = true
        tests = true
        javadoc = true
    }

    nexus {
        sign = true
        repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}